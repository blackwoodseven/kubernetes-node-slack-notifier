group 'com.blackwoodseven.kubernetes'

buildscript {
    ext.kotlin_version = '1.1.4-2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/jetbrains/spek' }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'io.github.microutils:kotlin-logging:1.4.5'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'org.slf4j:slf4j-simple:1.7.25'
    compile 'com.github.seancfoley:ipaddress:3.0.0'
    compile 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compile 'com.hazelcast:hazelcast:3.8.4'
    compile 'com.hazelcast:hazelcast-kubernetes:1.0.0'

    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.1'

    // Kotlin-test
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // Spek
    testCompile 'org.jetbrains.spek:spek-api:1.1.2'
    testCompile 'org.junit.platform:junit-platform-launcher:1.0.0-M4'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.2'

    // Kluent
    testCompile 'org.amshove.kluent:kluent:1.27'

    // Mockito
    testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"

    // JimFS
    testCompile 'com.google.jimfs:jimfs:1.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.blackwoodseven.kubernetes.node_watcher.MainKt'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

mainClassName = 'com.blackwoodseven.kubernetes.node_watcher.MainKt'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
}
